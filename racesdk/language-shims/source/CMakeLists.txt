
# 
# Copyright 2023 Two Six Technologies
# 
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
# 
#     http://www.apache.org/licenses/LICENSE-2.0
# 
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# 

################################################################################
# Setup SWIG
################################################################################

cmake_policy(SET CMP0078 NEW)
cmake_policy(SET CMP0086 NEW)
find_package(SWIG REQUIRED COMPONENTS go python)
set(UseSWIG_MODULE_VERSION 2)
include(${SWIG_USE_FILE})

if(ANDROID)
    # Have to ignore unresolved symbols since the go implementations of the generated functions
    # are not compiled here, but rather in the golang plugins. The Android toolchain sets the
    # --no-undefined linker flag by default, which results in undefined reference errors.
    set(CMAKE_SHARED_LINKER_FLAGS "-Wl,--warn-unresolved-symbols ${CMAKE_SHARED_LINKER_FLAGS}")
endif()

find_package(PythonLibs 3.7 EXACT REQUIRED)

set(CMAKE_SWIG_FLAGS "-DSWIGWORDSIZE64")
set(SWIG_COMPILE_FLAGS "${SWIG_COMPILE_FLAGS} -DSWIG_TYPE_TABLE=RaceSdk")


################################################################################
# Shims headers
################################################################################

set(SWIG_PYRUN_HEADER ${CMAKE_CURRENT_BINARY_DIR}/include/swigpyrun.h)

set(NETWORK_MANAGER_PY_HEADER ${CMAKE_CURRENT_BINARY_DIR}/include/networkManagerPluginBindingsPYTHON_wrap.h)
set(COMMS_PY_HEADER ${CMAKE_CURRENT_BINARY_DIR}/include/commsPluginBindingsPYTHON_wrap.h)
set(COMMS_GO_HEADER ${CMAKE_CURRENT_BINARY_DIR}/include/src/shims/commsPluginBindingsGO_wrap.h)

# Generate swigpyrun.h
add_custom_command(
  COMMAND ${CMAKE_COMMAND} -E make_directory ${CMAKE_CURRENT_BINARY_DIR}/include
  COMMAND ${SWIG_EXECUTABLE} -Wall -c++ -python -external-runtime ${SWIG_PYRUN_HEADER}
  OUTPUT ${SWIG_PYRUN_HEADER}
  COMMENT "Generating swigpyrun.h"
)

add_custom_target(swigpyrunHeader DEPENDS ${SWIG_PYRUN_HEADER})
add_custom_target(swigGoHeader DEPENDS ${SWIG_GO_HEADER})

# Create a target that other targets may used to include the swigpyrun header / other headers generated by swig
add_library(language-shims-headers INTERFACE)
add_dependencies(language-shims-headers swigpyrunHeader swigGoHeader raceSdkCommon)
# Specify these headers as SYSTEM headers so they don't generate warnings in consuming targets
target_include_directories(language-shims-headers SYSTEM INTERFACE
  $<BUILD_INTERFACE:${CMAKE_CURRENT_BINARY_DIR}/include>
  $<INSTALL_INTERFACE:include>
)
set_target_properties(language-shims-headers PROPERTIES
  PUBLIC_HEADER "${SWIG_PYRUN_HEADER};${NETWORK_MANAGER_PY_HEADER};${COMMS_PY_HEADER};${COMMS_GO_HEADER}"
)


################################################################################
# Python network manager
################################################################################


set_source_files_properties(networkManagerPluginBindings.i PROPERTIES CPLUSPLUS ON)
swig_add_library(networkManagerPluginBindings TYPE SHARED
  LANGUAGE python
  SOURCES networkManagerPluginBindings.i
  OUTPUT_DIR "${CMAKE_CURRENT_BINARY_DIR}/include/"
)
add_dependencies(networkManagerPluginBindings raceSdkCommon)
target_compile_definitions(networkManagerPluginBindings PUBLIC "SWIG_TYPE_TABLE=RaceSdk")
set_target_properties(networkManagerPluginBindings PROPERTIES
  SWIG_USE_TARGET_INCLUDE_DIRECTORIES TRUE
  COMPILE_OPTIONS "-Wno-error" # Clear warnings set globally via add_compile_options
)
target_include_directories(networkManagerPluginBindings PUBLIC ${PYTHON_INCLUDE_PATH})
target_link_options(networkManagerPluginBindings PUBLIC -Wl,--no-undefined)
swig_link_libraries(networkManagerPluginBindings ${PYTHON_LIBRARIES} language-shims-headers raceSdkCommon)


################################################################################
# Python Comms
################################################################################

set_source_files_properties(commsPluginBindings.i PROPERTIES CPLUSPLUS ON)
swig_add_library(commsPluginBindings TYPE SHARED
  LANGUAGE python
  SOURCES commsPluginBindings.i
  OUTPUT_DIR "${CMAKE_CURRENT_BINARY_DIR}/include/"
)
add_dependencies(commsPluginBindings raceSdkCommon)
target_compile_definitions(commsPluginBindings PUBLIC "SWIG_TYPE_TABLE=RaceSdk")
set_target_properties(commsPluginBindings PROPERTIES
  SWIG_USE_TARGET_INCLUDE_DIRECTORIES TRUE
  COMPILE_OPTIONS "-Wno-error" # Clear warnings set globally via add_compile_options
)
target_include_directories(commsPluginBindings PUBLIC ${PYTHON_INCLUDE_PATH})
target_link_options(commsPluginBindings PUBLIC -Wl,--no-undefined)
swig_link_libraries(commsPluginBindings ${PYTHON_LIBRARIES} language-shims-headers raceSdkCommon)


################################################################################
# Golang Comms
################################################################################

set_source_files_properties(commsPluginBindings.golang.i PROPERTIES
  TYPE MODULE
  CPLUSPLUS ON
  SWIG_FLAGS "-intgosize;64;-package;commsShims"
)
swig_add_library(commsPluginBindingsGolang TYPE SHARED
  LANGUAGE go
  SOURCES commsPluginBindings.golang.i
  OUTPUT_DIR "${CMAKE_CURRENT_BINARY_DIR}/include/src/shims"
)
add_dependencies(commsPluginBindingsGolang raceSdkCommon)
set_target_properties(commsPluginBindingsGolang PROPERTIES
  SWIG_USE_TARGET_INCLUDE_DIRECTORIES TRUE
  COMPILE_OPTIONS "-Wno-error" # Clear warnings set globally via add_compile_options
)

swig_link_libraries(commsPluginBindingsGolang language-shims-headers raceSdkCommon)


################################################################################
# Install
################################################################################

# Network Manager Python bindings
install(
  TARGETS networkManagerPluginBindings
  LIBRARY DESTINATION lib/race/python
)
install(
  FILES ${CMAKE_CURRENT_BINARY_DIR}/include/networkManagerPluginBindings.py
  DESTINATION lib/race/python
  COMPONENT sdk
)

# Comms Python bindings
install(
  TARGETS commsPluginBindings
  LIBRARY DESTINATION lib/race/python
)
install(
  FILES ${CMAKE_CURRENT_BINARY_DIR}/include/commsPluginBindings.py
  DESTINATION lib/race/python
  COMPONENT sdk
)

# Comms Golang bindings
install(
  TARGETS commsPluginBindingsGolang
  LIBRARY DESTINATION lib
)
install(
  FILES ${CMAKE_CURRENT_BINARY_DIR}/include/src/shims/commsPluginBindingsGolang.go
  DESTINATION go/src/shims
  COMPONENT sdk
)
